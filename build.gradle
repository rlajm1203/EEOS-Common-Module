plugins {
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.eeos'
version = '0.0.1'
description = 'EEOS-Common-Module'

ext {
    moduleVersions = [
            'common-dto'      : '0.0.1',
            'common-exception': '0.0.1',
            'common-web'      : '0.0.2',
            'common-jpa'      : '0.0.1',
            'common-domain'   : '0.0.1',
            'common-application' : '0.0.2'
    ]
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    group = rootProject.group
    version = rootProject.moduleVersions.getOrDefault(project.name, rootProject.version)

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/rlajm1203/EEOS-Common-Module")
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.0"
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = project.name
                    description = project.description ?: rootProject.description
                    url = 'https://github.com/rlajm1203/EEOS-Common-Module'
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/rlajm1203/EEOS-Common-Module")
                credentials {
                    username = findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
